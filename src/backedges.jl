# A lightly-modified version of the same function in Base
# Highlights argument types with color specified by highlighter(typ)
function show_tuple_as_call(@nospecialize(highlighter), io::IO, name::Symbol, @nospecialize(sig::Type), demangle=false #=, kwargs=nothing =#)
    if sig === Tuple
        print(io, demangle ? Base.demangle_function_name(name) : name, "(...)")
        return
    end
    tv = Any[]
    env_io = io
    while isa(sig, UnionAll)
        push!(tv, sig.var)
        env_io = IOContext(env_io, :unionall_env => sig.var)
        sig = sig.body
    end
    sig = (sig::DataType).parameters

    ft = sig[1]
    uw = Base.unwrap_unionall(ft)
    if ft <: Function && isa(uw,DataType) && isempty(uw.parameters) &&
            isdefined(uw.name.module, uw.name.mt.name) &&
            ft == typeof(getfield(uw.name.module, uw.name.mt.name))
        print(env_io, (demangle ? Base.demangle_function_name : identity)(uw.name.mt.name))
    elseif isa(ft, DataType) && ft.name === Type.body.name && !Core.Compiler.has_free_typevars(ft)
        f = ft.parameters[1]
        print(env_io, f)
    else
        print(env_io, "(::", ft, ")")
    end

    first = true
    print(io, "(")
    for i = 2:length(sig)  # fixme (iter): `eachindex` with offset?
        first || print(io, ", ")
        first = false
        printstyled(env_io, "::", sig[i], color=highlighter(sig[i]))
    end
    # if kwargs !== nothing
    #     print(io, "; ")
    #     first = true
    #     for (k, t) in kwargs
    #         first || print(io, ", ")
    #         first = false
    #         print(io, k, "::")
    #         show(io, t)
    #     end
    # end
    print(io, ")")
    Base.show_method_params(io, tv)
    nothing
end

function stripType(@nospecialize(typ))
    if isa(typ, UnionAll)
        typ = Base.unwrap_unionall(typ)
    elseif isa(typ, TypeVar) || isa(typ, Union)
        return typ
    end
    Base.isvarargtype(typ) && return typ
    return typ <: Type && length(typ.parameters) == 1 ? typ.parameters[1] : typ
end
nonconcrete_red(@nospecialize(typ)) = isconcretetype(stripType(typ)) ? :nothing : :red

const _emptybackedges = MethodInstance[]

# For handling stacktraces with ascend
struct IPFrames
    sfs::Vector{StackTraces.StackFrame}
end
function buildframes(bt)
    ipframes = IPFrames[]
    for ip in bt
        sfs = Base.StackTraces.lookup(ip)
        sf = sfs[end]
        sf.from_c && continue
        mi = sf.linfo
        isa(mi, Core.MethodInstance) || continue
        push!(ipframes, IPFrames(sfs))
    end
    return ipframes
end

# Extension API
backedges(mi::MethodInstance) = isdefined(mi, :backedges) ? mi.backedges : _emptybackedges
method(mi::MethodInstance) = mi.def
specTypes(mi::MethodInstance) = mi.specTypes
instance(mi::MethodInstance) = mi
nextnode(mi, edge) = edge

instance(sfs::Vector{StackTraces.StackFrame}) = isempty(sfs) ? Core.Compiler.Timings.ROOTmi : sfs[end].linfo::MethodInstance # we checked this type condition within `buildframes`
method(sfs::Vector{StackTraces.StackFrame}) = method(instance(sfs))

instance(ipframes::Vector{IPFrames}) = isempty(ipframes) ? Core.Compiler.Timings.ROOTmi : instance(ipframes[1].sfs)
backedges(ipframes::Vector{IPFrames}) = (ret = ipframes[2:end]; isempty(ret) ? () : (ret,))

function callstring(io::IO, obj)
    show_tuple_as_call(nonconcrete_red, IOContext(io, :color=>true), method(obj).name, specTypes(obj))
    return String(take!(io))
end
function callstring(io::IO, sfs::Vector{StackTraces.StackFrame})
    isempty(sfs) && return ""
    for i = 1:length(sfs)-1
        sf = sfs[i]
        print(io, sf.func, " at ", sf.file, ':', sf.line, " => ")
    end
    sf = sfs[end]
    return callstring(io, instance(sfs)) * string(" at ", sf.file, ':', sf.line)
end
callstring(io::IO, ipframes::Vector{IPFrames}) = isempty(ipframes) ? "" : callstring(io, ipframes[1].sfs)

struct Data{T}
    callstr::String
    nd::T
end

function treelist(mi)
    io = IOBuffer()
    str = callstring(io, mi)
    treelist!(Node(Data(str, instance(mi))), io, mi, "", Base.IdSet{typeof(instance(mi))}())
end
function treelist!(parent::Node, io::IO, mi, indent::AbstractString, visited::Base.IdSet)
    mi âˆˆ visited && return parent
    push!(visited, instance(mi))
    indent *= " "
    for edge in backedges(mi)
        str = indent * callstring(io, edge)
        child = Node(Data(str, instance(edge)), parent)
        treelist!(child, io, nextnode(mi, edge), indent, visited)
    end
    return parent
end
treelist!(::Node, ::IO, ::Nothing, ::AbstractString, ::Base.IdSet) = nothing

treelist(bt::Vector{Union{Ptr{Nothing}, Base.InterpreterIP}}) = treelist(buildframes(bt))
